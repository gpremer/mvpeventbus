subprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'code-quality'
  apply plugin: 'project-report'

  configurations {
    tools {
      description = 'Needed for build file tasks'
    }
  }

  repositories {
    mavenCentral()
    mavenRepo urls: 'http://guice-maven.googlecode.com/svn/trunk'
    mavenRepo urls: 'http://repository.opencastproject.org/nexus/content/groups/public/net/sourceforge'
  }

  dependencies {
    testCompile module('junit:junit:4.5')
    testCompile module('org.mockito:mockito-core:1.8.5') {
      dependency('org.objenesis:objenesis:1.0')
    }
    tools module('cobertura:cobertura:1.9.4.1') {
      dependency('oro:oro:2.0.8')  
      dependency('asm:asm:3.0')  
      dependency('asm:asm-tree:3.0')  
      dependency('log4j:log4j:1.2.9')  
    }
    testRuntime module('cobertura:cobertura:1.9.4.1')
  }

  checkstyleConfigFileName = "${rootDir}/config/checkstyle/application_checks.xml"

  checkstyleMain {
    ignoreFailures = true
  }

  checkstyleTest {
    ignoreFailures = true
    configFile = file("${rootDir}/config/checkstyle/junit.xml")
  }

  version = '1.0-rc5'

  jar {
    manifest.attributes provider: 'ICTects'
  }

  def instrumentationDir = sourceSets.main.classesDir
  def uninstrumentedDir = "${sourceSets.main.classesDir}-orig"
  def cobSerFile = "${project.buildDir}/cobertura.ser"

  test.doFirst {
    if ( project.computeCoverage ) {
      ant {
        delete(file:cobSerFile, failonerror:false) // delete from previous run
        delete(dir: uninstrumentedDir, failonerror:false) // clean backup
        copy(toDir: uninstrumentedDir) { fileset(dir: instrumentationDir) } // make backup
        taskdef(resource:'tasks.properties', classpath: configurations.tools.asPath)
        'cobertura-instrument'(datafile:cobSerFile) {
          fileset(dir: instrumentationDir, includes:"**/*.class")
        }
      }
    }
  }

  test {
    ignoreFailures = true
    systemProperties ["net.sourceforge.cobertura.datafile"] = cobSerFile
  }

  test.doLast {
    if ( project.computeCoverage && new File(uninstrumentedDir).exists() && new File(cobSerFile).exists()) {
      def outputDir = "${project.buildDirName}/${coverageDir}"
      ant {
        delete(file: instrumentationDir) // remove work directory
        move(file: uninstrumentedDir, tofile: instrumentationDir) // put original back
	['xml', 'html'].each {repFormat ->
	  'cobertura-report'(destdir: outputDir, 
			     format: repFormat, 
			     datafile: cobSerFile) { 
	    sourceSets.main.allJava.addToAntBuilder(ant, 'fileset')
	  }
	}
      }
    }
  }
}

project.computeCoverage = false

configurations {
  tools {
    description = 'Needed for build file tasks'
  }
}

repositories {
  mavenCentral()
  mavenRepo urls: 'http://repository.opencastproject.org/nexus/content/groups/public/net/sourceforge'
}

dependencies {
  tools module('cobertura:cobertura:1.9.4.1') {
    dependency('oro:oro:2.0.8')  
    dependency('asm:asm:3.0')  
    dependency('asm:asm-tree:3.0')  
    dependency('log4j:log4j:1.2.9')  
  }
}

task setupCobertura << {
  ant.taskdef(resource:'tasks.properties', classpath: configurations.tools.asPath)
}

task coverage(dependsOn: setupCobertura) << {
  project.computeCoverage = true
}

task assembleCoverage(dependsOn: coverage) << {
  def dataFile = "${project.buildDirName}/cobertura.ser"
  def destDir = "${project.buildDirName}/${coverageDir}"
  ant {
    delete(file: dataFile, failonerror: false) // remove from previous run
    'cobertura-merge'(datafile: dataFile) {
      fileset(dir: '.', includes: "**/cobertura.ser")
    }
    ['xml', 'html'].each {repFormat ->
      'cobertura-report'(destdir: destDir, 
			 format: repFormat, 
			 datafile: dataFile) {
	subprojects.each { subproject -> subproject.sourceSets.main.allJava.addToAntBuilder(ant, 'fileset') }
      }
    }
  }
}

// Hudson expects the test results to be newer than the build start time, but gradle skips executing tests if the main source was not changed
task fixupTestDatesForHudson << {
  testReportFiles = fileTree(dir: '.', include: '**/build/test-results/TEST*.xml')
  testReportFiles.each { it.setLastModified(System.currentTimeMillis()) }
}
